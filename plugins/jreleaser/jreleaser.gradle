/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2024 The JReleaser authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'application'
    id 'org.jreleaser.jdks'
}

application {
    mainClass = 'org.jreleaser.cli.Main'
}

jar {
    manifest {
        attributes([
            'Main-Class': 'org.jreleaser.cli.Main',
            'Automatic-Module-Name': project.findProperty('automatic.module.name')
        ])
    }
}

startScripts {
    classpath = files('$APP_HOME/lib/*')
}

config {
    info {
        specification { enabled = true }
    }
}

configurations {
    graalvm
}

dependencies {
    api project(':jreleaser-engine')
    graalvm project(':jreleaser-graalvm-sdk')
    //config
    api project(':jreleaser-config-yaml')
    api project(':jreleaser-config-toml')
    api project(':jreleaser-config-json')

    annotationProcessor "info.picocli:picocli-codegen:$picocliVersion"
    api "info.picocli:picocli:$picocliVersion"
    runtimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

processResources {
    inputs.property('version', project.version)
    filesMatching(['**/Banner*.properties']) {
        expand(
            'version': project.version,
            'id': 'jreleaser',
            'name': 'jreleaser'
        )
    }
}

distributions {
    main {
        contents {
            from('../..') {
                include 'licenses/*'
                include 'LICENSE'
                include 'NOTICE'
                include 'VERSION'
            }
        }
    }
}

jdks {
    zulu21Linux {
        platform = 'linux-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-linux_x64.zip'
        checksum = '55361ac2bb173f925ae5b78af1ea339888679ac0e7aee27c0a83a237de95a605'
    }
    zulu21LinuxArm {
        platform = 'linux-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-linux_aarch64.tar.gz'
        checksum = 'ce7df1af5d44a9f455617c4b8891443fbe3e4b269c777d8b82ed66f77167cfe0'
    }
    zulu21LinuxMusl {
        platform = 'linux_musl-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-linux_musl_x64.tar.gz'
        checksum = 'ec8ca330eb65c626f9045414ec1646970a66e9afa8a8d91f8db8d5d5b0e18d51'
    }
    zulu21LinuxMuslArm {
        platform = 'linux_musl-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-linux_musl_aarch64.tar.gz'
        checksum = '418bef47fea1e55fcd1bee0fbc7eae876b8b160a868857c5e16a082669505144'
    }
    zulu21Windows {
        platform = 'windows-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-win_x64.zip'
        checksum = 'f7cc15ca17295e69c907402dfe8db240db446e75d3b150da7bf67243cded93de'
    }
    //zulu21WindowsArm {
    //    platform = 'windows-aarch64'
    //    url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-win_aarch64.zip'
    //    checksum = '1a9d4ab3b52ce3db7354691163e5a36979af9c37d80c3c490c12af07de21d8f3'
    //}
    zulu21Osx {
        platform = 'osx-x86_64'
        url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-macosx_x64.zip'
        checksum = '1fde3ae20ad28f9a1820524d7699f4a349382af1dc7d339f27acc75ca0dd27ab'
    }
    zulu21OsxArm {
        platform = 'osx-aarch64'
        url = 'https://cdn.azul.com/zulu/bin/zulu21.32.17-ca-jdk21.0.2-macosx_aarch64.zip'
        checksum = 'b7131bfe20d258fee795b2a175821fca697aa90a8b34db303bbe364957de6ff7'
    }
    graal21Osx {
        platform = 'osx-x86_64'
        url = 'https://github.com/graalvm/graalvm-ce-builds/releases/download/jdk-21.0.2/graalvm-community-jdk-21.0.2_macos-x64_bin.tar.gz'
        checksum = '7a8aa93fa45d1721908477abf4732a32637d420ffcb66ada9fb6456440b0d9e1'
    }
    graal21Linux {
        platform = 'linux-x86_64'
        url = 'https://github.com/graalvm/graalvm-ce-builds/releases/download/jdk-21.0.2/graalvm-community-jdk-21.0.2_linux-x64_bin.tar.gz'
        checksum = 'b048069aaa3a99b84f5b957b162cc181a32a4330cbc35402766363c5be76ae48'
    }
    //graal17LinuxArm {
    //    platform = 'linux-aarch64'
    //    url = 'https://github.com/graalvm/graalvm-ce-builds/releases/download/vm-22.0.0.2/graalvm-ce-java17-linux-aarch64-22.0.0.2.tar.gz'
    //    checksum = 'c7d78387d2a144944f26773697c1b61d3478a081a1c5e7fc20f47f1f5f3c82c7'
    //}
    graal21Windows {
        platform = 'windows-x86_64'
        url = 'https://github.com/graalvm/graalvm-ce-builds/releases/download/jdk-21.0.2/graalvm-community-jdk-21.0.2_windows-x64_bin.zip'
        checksum = 'e17b7bead097bf372a5c75df17815b0a2f30b777a019d25eff7706b21421f7fa'
    }
}

copyDependencies {
    dependsOn classes
    inputs.files(configurations.runtimeClasspath)
    configuration = 'runtimeClasspath'
}

task copyGraalvmJavaSdk(type: Copy) {
    from configurations.graalvm
    into 'build/dependencies/graalvm'
}

assemble.dependsOn copyDependencies, copyGraalvmJavaSdk