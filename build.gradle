/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
    info {
        description = 'jreleaser'
        inceptionYear = '2020'
        tags = ['release']

        specification {
            enabled = true
        }

        implementation {
            enabled = true
        }
    }

    docs {
        javadoc {
            autoLinks {
                enabled = false
            }
        }
    }

    bintray {
        enabled = false
    }
}

allprojects {
    repositories {
        gradlePluginPortal()
        jcenter()
        mavenLocal()
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

subprojects {
    if (project.name != 'guide') {
        config {
            info {
                description = project.project_description
            }

            bintray {
                enabled = true
                name    = project.name
            }
        }

        tasks.withType(JavaCompile) { JavaCompile c ->
            c.sourceCompatibility = JavaVersion.VERSION_1_8
            c.targetCompatibility = JavaVersion.VERSION_1_8
        }
        tasks.withType(GroovyCompile) { GroovyCompile c ->
            c.sourceCompatibility = JavaVersion.VERSION_1_8
            c.targetCompatibility = JavaVersion.VERSION_1_8
        }

        if (project.projectDir.parentFile.name != 'plugins' &&
                project.name != 'jreleaser-tool-provider') {
            jar {
                multiRelease = true
                moduleInfoPath = 'src/main/module/module-info.java'
            }
        }

        license {
            exclude('build/**')
            exclude('**/*.tpl')
        }

        dependencies {
            testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
            testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"

            testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        }

        test {
            useJUnitPlatform()
        }
    }
}
